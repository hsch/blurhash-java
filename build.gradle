plugins {
    id "java"
    id "maven"
    id "signing"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "junit:junit:4.12"
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                group = "io.trbl"
                artifactId = "blurhash"
                archivesBaseName = "blurhash"
                version = "1.0.0"
                name "BlurHash for Java"
                description "A BlurHash encoder implementation in Java"
                url "https://github.com/hsch/blurhash-java"
                scm {
                    url "https://github.com/hsch/blurhash-java"
                    connection "scm:git:https://github.com/hsch/blurhash-java.git"
                    developerConnection "scm:git:git@github.com:hsch/blurhash-java.git"
                }
                licenses {
                    license {
                        name "MIT"
                        url "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id "hendrik"
                        name "Hendrik"
                        email "hendrik@h5k.org"
                    }
                }
            }
        }
    }
}
